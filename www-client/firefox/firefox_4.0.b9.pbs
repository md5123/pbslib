# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor:  Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: blasse <koralik(at)gmail(dot)com>

pkgname=firefox-beta
pkgver=4.0b10pre
pkgrel=3
_build=1
pkgdesc="Mozilla Firefox beta (XULRunner independent)"
url="http://www.mozilla.org/projects/firefox"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')

depends=('gtk2>=2.20.1' 'gcc-libs>=4.5.0' 'libidl2>=0.8.13' 'mozilla-common'
         'libxt' 'hunspell>=1.2.8' 'startup-notification>=0.10' 'zlib'
         'libnotify>=0.4' 'mime-types' 'dbus-glib>=0.86' 'desktop-file-utils'
         'libpng>=1.4.0' 'alsa-lib>=1.0.23' 'libevent>=1.4' 'sqlite3>=3.7.4'
         'libxrender' 'fontconfig' 'glib2')

makedepends=('autoconf2.13' 'gcc>=4.5.0' 'zip' 'pkgconfig' 'diffutils'
             'libgnomeui>=2.24.1' 'python2' 'wireless_tools' 'mesa' 'yasm')

optdepends=('libgnomeui: GNOME integration and MIME handling'
            'wireless_tools: Location-aware browsing'
            'python: pyxpcom')

provides=("firefox=$pkgver")
conflicts=("firefox")

#source=("ftp://ftp.mozilla.org/pub/mozilla.org/firefox/nightly/$pkgver-candidates/build$_build/source/firefox-$pkgver.source.tar.bz2"
source=("http://hg.mozilla.org/mozilla-central/archive/tip.tar.bz2"
        'mozconfig' 'firefox.desktop' 'firefox-safe.desktop'
        'fix-mozilla-launcher.patch' 'mozilla-firefox-1.0-lang.patch'
        'hugfox.patch' 'cairo-lcd-filter.patch' 'cairo-lcd-filter-build.patch'
        'cairo-respect-fontconfig.patch' 'cairo-1.10.0-buggy_gradients.patch')

build() {
  cd "$srcdir"

  # rename dir if this is a hg snapshot
  for _f in mozilla-central-*; do 
    if [[ -d $_f ]]; then
      rm -rf mozilla-central
      mv -T $_f mozilla-central
    fi
  done

  cd mozilla-central

  cp "$srcdir/mozconfig" .mozconfig

msg2 "Patching..."

  # Don't strip if the user doesn't want us to... ;p
  if [ "$(check_option strip)" = "n" ]; then
    sed -e 's/--enable-strip/--disable-strip/' \
        -e 's/--enable-install-strip/--disable-install-strip/' \
        -i .mozconfig
  fi

  # Fix stub launcher - Arch
  patch -Np0 -i "$srcdir/fix-mozilla-launcher.patch"

  # Use LANG environment variable to choose locale
  patch -Np1 -i "$srcdir/mozilla-firefox-1.0-lang.patch"

  # Enable hugfox branding
  (
    cd browser/branding
    rm -rf hugfox
    cp -r nightly hugfox
    cd hugfox
    patch -Np1 -i "$srcdir/hugfox.patch"
  )

  # Do some naughty things to the in-tree Cairo
  (
    cd gfx/cairo/cairo
    patch -Np1 -i "$srcdir/cairo-1.10.0-buggy_gradients.patch"
    patch -Np1 -i "$srcdir/cairo-lcd-filter.patch"
    #patch -Np1 -i "$srcdir/cairo-respect-fontconfig.patch"
  )
  patch -Np1 -i "$srcdir/cairo-lcd-filter-build.patch"

msg2 "Configuring..."

  # The hash-style and as-needed flags are in Arch defaults anyways,
  # and the other optimization flags are almost definitely safe.
  export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-$pkgver -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"

  # Use Python 2
  export PYTHON="/usr/bin/python2"

  autoconf-2.13

msg2 "Building..."

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "$srcdir"/mozilla-central

  make -j1 -f client.mk DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/mozilla-central/browser/branding/hugfox/mozicon128.png" \
    "$pkgdir/usr/share/pixmaps/firefox.png"

  install -Dm644 "$srcdir/firefox.desktop" \
    "$pkgdir/usr/share/applications/firefox.desktop"

  install -Dm644 "$srcdir/firefox-safe.desktop" \
    "$pkgdir/usr/share/applications/firefox-safe.desktop"

  # delete included dictionaries and symlink to /usr/share/myspell/dicts
  # so that we use the hunspell-* dictionaries
  rm -rf "$pkgdir/usr/lib/firefox-$pkgver/dictionaries"
  ln -sf /usr/share/myspell/dicts "$pkgdir/usr/lib/firefox-$pkgver/dictionaries"

  # Remove devel stuff.
  rm -rf "$pkgdir/usr/include/"
  rm -rf "$pkgdir/usr/lib/firefox-devel-$pkgver/"
  rm -rf "$pkgdir/usr/share/idl/"
}

md5sums=('57cd9596c795479c2c15786d91d51b02'
         'cd424b67807dc13f998d48da95a35259'
         'ba96924ece1d77453e462429037a2ce5'
         '6f38a5899034b7786cb1f75ad42032b8'
         '63eee2d1da3b43c9d604f2253f242f40'
         'bd5db57c23c72a02a489592644f18995'
         '6404c6302fe10c9950d62f3f6e11ae99'
         '5901de7f82343444230a81f831bfa440'
         'c6232886cfeb75b87b22e7a7b326f893'
         '79f7c141c49f3d65ab308cc706d50914'
         '9b323790dab003e228c6955633cb888e')
